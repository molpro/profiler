add_custom_target(profiler-examples ALL COMMENT "Builds all profiler examples")

list(APPEND srcs profiler-example.cpp profiler-singleton-example.cpp)
if (FORTRAN)
    list(APPEND srcs profiler-fortran-example.F90)
endif ()
# Fortran is either serial or parallel
foreach (example ${srcs})
    set(compile_serial ON)
    set(compile_parallel OFF)
    string(FIND "${example}" .F i)
    if (i EQUAL -1)
        set(is_fortran OFF)
    else ()
        set(is_fortran ON)
    endif ()
    if (MPI)
        set(compile_parallel ON)
        if (is_fortran)
            set(compile_serial OFF)
        endif ()
    endif ()
    get_filename_component(name ${example} NAME_WE)
    if (compile_serial)
        add_executable(${name}-serial ${example} timing_molpro.cpp)
        target_link_libraries(${name}-serial PUBLIC molpro::Profiler)
        target_compile_options(${name}-serial PRIVATE -UMOLPRO_PROFILER_MPI)
        add_test(NAME ${name}-serial COMMAND ${name}-serial WORKING_DIRECTORY ${CMAKE_BINARY_DIR})
        set_tests_properties(${name}-serial PROPERTIES TIMEOUT 60)
        add_dependencies(profiler-examples ${name}-serial)
    endif ()
    if (compile_parallel)
        add_executable(${name}-mpi ${example} timing_molpro.cpp)
        target_link_libraries(${name}-mpi PUBLIC molpro::Profiler)
        target_compile_definitions(${name}-mpi PRIVATE HAVE_MPI_H)
        add_test(NAME ${name}-mpi COMMAND ${MPIEXEC_EXECUTABLE} ${MPIEXEC_NUMPROC_FLAG} ${MPIEXEC_MAX_NUMPROCS}
                ${MPIEXEC_PREFLAGS} ${CMAKE_CURRENT_BINARY_DIR}/${name}-mpi ${MPIEXEC_POSTFLAGS}
                WORKING_DIRECTORY ${CMAKE_BINARY_DIR})
        set_tests_properties(${name}-mpi PROPERTIES TIMEOUT 60)
        add_dependencies(profiler-examples ${name}-mpi)
    endif ()
endforeach ()

add_executable(readme-example-serial readme-example.cpp)
target_link_libraries(readme-example-serial PUBLIC molpro::Profiler)
target_compile_options(readme-example-serial PRIVATE -UMOLPRO_PROFILER_MPI)
add_test(NAME readme-example-serial COMMAND readme-example-serial WORKING_DIRECTORY ${CMAKE_BINARY_DIR})
set_tests_properties(readme-example-serial PROPERTIES TIMEOUT 60)
add_dependencies(profiler-examples readme-example-serial)
